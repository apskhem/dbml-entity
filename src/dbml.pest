WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ single_line_comment | multi_line_comment }

// comments
single_line_comment = { "//" ~ (!"\n" ~ ANY)* }
multi_line_comment = { "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// literals
double_quoted_value = @{ (!"\"" ~ ANY)* }
double_quoted_string = { "\"" ~ double_quoted_value ~ "\"" }
single_quoted_value = @{ (!"'" ~ ANY)* }
single_quoted_string = { "'" ~ single_quoted_value ~ "'"}
backquoted_quoted_value = @{ (!"`" ~ ANY)* }
backquoted_quoted_string = { "`" ~ backquoted_quoted_value ~ "`" }
triple_quoted_value = @{ (!"'''" ~ ANY)* }
triple_quoted_string = { "'''" ~ triple_quoted_value ~ "'''" }

var = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

ident = @{ var | double_quoted_string }

// values
string_value = { triple_quoted_string | single_quoted_string }
integer = @{ ASCII_DIGIT+ }
decimal = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }
number_value = { integer | decimal }
value = { string_value | number_value }

// top-level ident
decl_ident = { ident ~ ("." ~ ident)? }

// ref block
relation = @{ "<>" | "-" | ">" | "<" }
ref_composition = { ident | ("(" ~ ident ~ ("," ~ ident)* ~ ")") }
ref_ident = { ident ~ "." ~ (ident ~ ".")? ~ ref_composition }
ref_inline = { "ref" ~ ":" ~ relation ~ ref_ident }
ref_stmt = { ref_ident ~ relation ~ ref_ident }
ref_short = { "Ref" ~ ":" ~ ref_stmt }
ref_block = { "Ref" ~ "{" ~ ref_stmt ~ "}" }
ref_decl = { ref_block | ref_short }

// note block
note_inline = { "note" ~ ":" ~ string_value }
note_short = { "Note" ~ ":" ~ string_value }
note_block = { "Note" ~ "{" ~ string_value ~ "}" }
note_decl = { note_short | note_block }

// enum block
enum_attribute = { note_inline }
enum_settings = { "[" ~ (col_attribute ~ ("," ~ col_attribute)*)? ~ "]" }
enum_value = { ident ~ enum_settings? }
enum_block = { "{" ~ enum_value* ~ "}" }
enum_decl = { "enum " ~ decl_ident ~ enum_block }

// table block
col_default = { "default" ~ ":" ~ string_value }
col_attribute = { "unique" | "primary key" | "pk" | "null" | "not null" | col_default | note_inline | ref_inline }
col_settings = { "[" ~ (col_attribute ~ ("," ~ col_attribute)*)? ~ "]" }
col_type_arg = { "(" ~ (value ~ ("," ~ value)*)? ~ ")" }
col_type_single = { (var ~ col_type_arg?) | ("\"" ~ (var ~ col_type_arg?) ~ "\"") }
col_type_array = { "\"" ~ (var ~ col_type_arg?) ~ "[]" ~ "\"" }
col_type = { col_type_single | col_type_array }
table_alias = { "as " ~ ident }
table_col = { ident ~ col_type ~ col_settings? }
table_block = { "{" ~ (table_col | note_decl)* ~ "}" }
table_decl = { "Table " ~ decl_ident ~ table_alias? ~ table_block }

// table group block
table_group_block = { "{" ~ decl_ident* ~ "}" }
table_group_decl = { "TableGroup " ~ ident ~ table_group_block }

// project block
project_key = @{ "database_type" }
project_stmt = { project_key ~ ":" ~ string_value }
project_block = { "{" ~ (project_stmt | note_decl)* ~ "}" }
project_decl = { "Project " ~ ident ~ project_block }

schema = { SOI ~ (project_decl | table_decl | enum_decl | ref_decl | table_group_decl)* ~ EOI }
